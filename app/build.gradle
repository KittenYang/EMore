apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'

apply from: '../key.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.caij.emore"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 3
        versionName "1.0-beta2"
    }

    sourceSets.main {
        jniLibs.srcDirs = ['libs']
    }

    signingConfigs {
        debug {

        }

        release {
            if (key_store != null) {
                storeFile file(key_store)
                storePassword store_password
                keyAlias key_alias
                keyPassword key_password
            }
        }
    }

    buildTypes {

        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "emore-v${defaultConfig.versionName}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

//    splits {
//        // Split apks on build target ABI, view all options for the splits here:
//        // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
//        abi {
//            enable true
//            reset()
//            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
//        }
//    }

    productFlavors {

        default_abi{}

        armeabi {
            ndk {
                abiFilters ("armeabi")
            }
        }

        x86_armeabi {
            ndk {
                abiFilters ("x86", "armeabi")
            }
        }
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportVersion}"
    compile "com.android.support:design:${rootProject.ext.supportVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportVersion}"
    compile "com.yqritc:recyclerview-flexibledivider:${rootProject.ext.flexibledividerVersion}"
    compile "com.getbase:floatingactionbutton:${rootProject.ext.floatingactionbuttonVersion}"
    compile("com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitVersion}") {
        exclude module: 'okhttp'
    }
    compile "com.squareup.retrofit2:adapter-rxjava:${rootProject.ext.retrofitVersion}"
//    compile("com.squareup.retrofit2:converter-gson:${rootProject.ext.retrofitVersion}") {
//        exclude module: 'gson'
//    }
    compile "com.google.code.gson:gson:${rootProject.ext.gsonVersion}"
    compile "io.reactivex:rxandroid:${rootProject.ext.rxAndroidVersion}"
    compile "io.reactivex:rxjava:${rootProject.ext.rxJavaVersion}"
    compile "com.squareup.okhttp3:okhttp:${rootProject.ext.okhttpVersion}"
//    compile "com.squareup.okhttp3:logging-interceptor:${rootProject.ext.okhttpVersion}"
    compile "com.github.bumptech.glide:glide:${rootProject.ext.glideVersion}"
    compile "com.github.bumptech.glide:okhttp3-integration:${rootProject.ext.glideIntegrationVersion}@aar"
    compile "org.greenrobot:greendao:${rootProject.ext.greendaoVersion}"
    compile "com.github.chrisbanes:PhotoView:${rootProject.ext.photoViewVersion}"
    compile "org.jsoup:jsoup:${rootProject.ext.jsoupVersion}"
    compile("com.lapism:searchview:${rootProject.ext.searchviewVersion}") {
        exclude module: 'design'
    }
    compile "com.tencent.bugly:crashreport:${rootProject.ext.crashreportVersion}"
    //    compile "com.tencent.bugly:nativecrashreport:${rootProject.ext.nativecrashreportVersion}"

    compile 'com.github.Caij:ProgressView:9ff7176f6b'

    compile "com.jakewharton:butterknife:${rootProject.ext.butterknifeVersion}"
    apt "com.jakewharton:butterknife-compiler:${rootProject.ext.butterknifeVersion}"

    //dagger
    apt "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    compile "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"

    debugCompile "com.facebook.stetho:stetho:${rootProject.ext.stethoVersion}"
    debugCompile "com.facebook.stetho:stetho-okhttp3:${rootProject.ext.stethoVersion}"
    debugCompile "com.squareup.leakcanary:leakcanary-android:${rootProject.ext.leakcanaryVersion}"

}
